directive ::= primary_directive ( operator expression )?

primary_directive ::=
  ( "-" | "~" )?
  (
    // ✅ ( close < boll.lower ) & ( kdj.j < 0 )
    // ✅ ( close ) > boll.lower
    // ✅ `- (boll.upper - close) < close`
    wrapped_directive
    // ✅ `- boll.upper < close`
    // ✅ `- boll`
    | command
  )

wrapped_directive ::= "(" directive ")"

operator ::= logical_operator | math_operator

// Logical (Boolean) operators
logical_operator ::= '//' | '\\' | '><' | '<' | '<=' | '==' | '!=' | '>=' | '>' | '|' | '&'
// Math operators
math_operator ::= '/' | '*' | '+' | '-'

expression ::= number | primary_directive

command ::= command_name ( ":" param ( "," param )* )? ( "@" series ( "," series )* )?

command_name ::= main_command_name ( "." sub_command_name )?

// fallback to column
main_command_name ::= ALPHABETS
sub_command_name ::= ALPHABETS

// - empty_string: default value
// - number: number value
param ::= EMPTY_STRING | number

// - empty_string: default value
// - ALPHABETS: column name
// - directive: nested directive
series ::= EMPTY_STRING | ALPHABETS | wrapped_directive

number ::= DIGIT+ ( "." DIGIT+ )?
DIGIT  ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"

ALPHABETS ::= [a-zA-Z_][a-zA-Z0-9_]*
EMPTY_STRING ::= ""
