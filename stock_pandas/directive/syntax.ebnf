// Example:
// ✅ 1. ( close < boll.lower ) & ( kdj.j < 0 )
// ✅ 2. ( close ) > boll.lower
// ✅ 3. - (boll.upper - close) < close
// ✅ 4. - boll.upper < close
// ✅ 5. - boll
// ✅ 6. - bbw / 4 * ma:20 + 2 * ma:20
// ✅ 7. - bbw / 4 * ( ma:20 + - 2 * ma:20 ) < close

// Top level: lowest priority operators (Style operators: '//' | '\\' | '><')
directive ::= bitwise_expression ( style_operator bitwise_expression )*

// Bitwise operators ('|' | '&' | '^')
bitwise_expression ::= logical_expression ( bitwise_operator logical_expression )*

// Logical operators ('<' | '<=' | '==' | '!=' | '>=' | '>' | '||' | '&&')
logical_expression ::= addition_expression ( logical_operator addition_expression )*

// Addition operators ('+' | '-')
addition_expression ::= multiplication_expression ( addition_operator multiplication_expression )*

// Multiplication operators (highest priority: '*' | '/')
multiplication_expression ::= primary_expression ( multiplication_operator primary_expression )*

// Primary expressions (no operators)
primary_expression ::= number | primary_directive

primary_directive ::=
  ( "-" | "~" )?
  (
    wrapped_directive | command
  )

wrapped_directive ::= "(" directive ")"

style_operator ::= '//' | '\\' | '><'

// Bitwise operators
bitwise_operator ::= '|' | '&' | '^'

// Logical (Boolean) operators
logical_operator ::=  '<' | '<=' | '==' | '!=' | '>=' | '>' | '||' | '&&'

// Math operators
addition_operator ::= '+' | '-'
multiplication_operator ::= '*' | '/'


command ::= command_name ( ":" param ( "," param )* )? ( "@" series ( "," series )* )?

command_name ::= main_command_name ( "." sub_command_name )?

// fallback to column
main_command_name ::= ALPHABETS
sub_command_name ::= ALPHABETS

// - empty_string: default value
// - number: number value
param ::= EMPTY_STRING | number

// - empty_string: default value
// - ALPHABETS: column name
// - directive: nested directive
series ::= EMPTY_STRING | ALPHABETS | wrapped_directive

number ::= DIGIT+ ( "." DIGIT+ )?
DIGIT  ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"

ALPHABETS ::= [a-zA-Z_][a-zA-Z0-9_]*
EMPTY_STRING ::= ""
